apply from: project.rootProject.file("flavors.gradle")
apply from: project.rootProject.file('dependencies.gradle')

buildscript {
    ext {
        gradleVersion = '3.6.3'
        kotlinVersion = '1.3.61'

        javaTargetVersion = 1.8
        sdkTargetVersion = 29
        sdkMinVersion = 21

        testInstrumentationClass = "androidx.test.runner.AndroidJUnitRunner"
        kotlin_version = '1.3.61'
    }

    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android') ?: project.plugins.findPlugin('android-library')) {
            android {
                compileSdkVersion sdkTargetVersion

                compileOptions {
                    sourceCompatibility javaTargetVersion
                    targetCompatibility javaTargetVersion
                }

                kotlinOptions {
                    jvmTarget = "$javaTargetVersion"
                }

                defaultConfig {
                    minSdkVersion sdkMinVersion
                    targetSdkVersion sdkTargetVersion
                    testInstrumentationRunner testInstrumentationClass
                }

                languagesFlavors.each { name ->
                    sourceSets {
                        main.java.srcDir "${project.projectDir}/flavorSrc/$name/kotlin"
                        main.res.srcDir "${project.projectDir}/flavorSrc/$name/res"
                        main.assets.srcDir "${project.projectDir}/flavorSrc/$name/assets"
                    }
                }
                sourceSets {
                    main.java.srcDirs += 'src/main/kotlin'
                    test.java.srcDirs += 'src/test/kotlin'
                    androidTest.java.srcDirs += 'src/androidTest/kotlin'
                }
            }
        }
    }
}
